terraform {
  required_providers {
    yandex = {
      source = "yandex-cloud/yandex"
    }
  }
}

resource "yandex_compute_instance" "compute_instance" {
  name        = var.compute_instance
  platform_id = var.platform_id
  zone        = var.zone

  resources {
    cores  = var.cores
    memory = var.memory
  }

  boot_disk {
    disk_id = yandex_compute_disk.compute_disk.id
  }

  network_interface {
    subnet_id      = yandex_vpc_subnet.vpc_subnet.id
    nat            = var.nat
    nat_ip_address = yandex_vpc_address.vpc_address.external_ipv4_address[0].address
  }

  metadata = {
    user-data = "${file("${path.module}/users.yaml")}"
  }
} 

resource "yandex_compute_disk" "compute_disk" {
  name     = var.compute_disk
  size     = var.disk_size
  zone     = var.zone
  image_id = var.image_id
}

resource "yandex_vpc_address" "vpc_address" {
  name = var.vpc_address
  external_ipv4_address {
    zone_id = var.zone
  }
}

resource "yandex_vpc_network" "vpc_network" {
  name = var.vpc_network
}

resource "yandex_vpc_subnet" "vpc_subnet" {
  name           = var.vpc_subnet
  zone           = var.zone
  network_id     = yandex_vpc_network.vpc_network.id
  v4_cidr_blocks = var.v4_cidr_block
}

resource "yandex_iam_service_account" "iam_service_account" {
  name = var.iam_service_account
}

resource "yandex_resourcemanager_folder_iam_member" "editor" {
  folder_id = var.folder_id
  role      = "editor"
  member    = "serviceAccount:${yandex_iam_service_account.iam_service_account.id}"
}

resource "yandex_resourcemanager_folder_iam_member" "images-puller" {
  folder_id = var.folder_id
  role      = "container-registry.images.puller"
  member    = "serviceAccount:${yandex_iam_service_account.iam_service_account.id}"
}

resource "yandex_kubernetes_cluster" "kubernetes_cluster" {
 network_id = yandex_vpc_network.vpc_network.id
 master {
   master_location {
     zone      = var.zone
     subnet_id = yandex_vpc_subnet.vpc_subnet.id
   }
 }
 service_account_id      = yandex_iam_service_account.iam_service_account.id
 node_service_account_id = yandex_iam_service_account.iam_service_account.id
   depends_on = [
     yandex_resourcemanager_folder_iam_member.editor,
     yandex_resourcemanager_folder_iam_member.images-puller
   ]
}